
program     : (taggable | tag_stmt | swap_stmt | if_stmt | while_stmt | print_stmt)*

taggable    : (TAG)? assign_stmt

assign_stmt : IDENTIFIER ("[" expr "]")? "=" (vector | expr)

tag_stmt    : TAG

swap_stmt   : "swap" IDENTIFIER ("[" expr "]")? IDENTIFIER ("[" expr "]")?

if_stmt     : "if" comparison "{" (program)* "}" ("else" "{" (program)* "}")?

while_stmt  : "while" comparison "{" (program)* "}"

print_stmt  : "print" comparison

comparison  : expr
            | expr "==" comparison                -> eq
            | expr "!=" comparison                -> neq
            | expr "<" comparison                 -> lt
            | expr ">" comparison                 -> gt

expr        : term
            | term "+" expr                 -> add
            | term "-" expr                 -> sub

term        : factor
            | factor "*" term             -> mul
            | factor "/" term             -> div

factor      : SIGNED_NUMBER                 -> number
            | IDENTIFIER ("[" expr "]")?    -> identifier
            | "True"                        -> true
            | "False"                       -> false
            | "(" expr ")"

vector      : "[" (expr ("," expr)*)? "]"

TAG         : /@[a-zA-Z]+/

COMMENT     : ("--" | "->") /[^\n]*/

%import common.CNAME -> IDENTIFIER
%import common.SIGNED_NUMBER
%import common.WS

%ignore WS
%ignore COMMENT
