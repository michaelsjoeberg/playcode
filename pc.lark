
program     : (taggable | tag_stmt | swap_stmt | if_stmt | while_stmt | print_stmt)*

taggable    : (TAG)? assign_stmt

assign_stmt : identifier "=" (vector | expr)

tag_stmt    : TAG

swap_stmt   : "swap" identifier identifier

if_stmt     : "if" comparison "{" (program)* "}" ("else" "{" (program)* "}")?

while_stmt  : "while" comparison "{" (program)* "}"

print_stmt  : "print" comparison (assert)?

comparison  : expr
            | expr "==" comparison                -> eq
            | expr "!=" comparison                -> neq
            | expr "<" comparison                 -> lt
            | expr ">" comparison                 -> gt

expr        : term
            | term "+" expr                 -> add
            | term "-" expr                 -> sub

term        : factor
            | factor "*" term             -> mul
            | factor "/" term             -> div

factor      : SIGNED_NUMBER                 -> number
            | identifier
            | "True"                        -> true
            | "False"                       -> false
            | "(" expr ")"

identifier  : CNAME ("[" expr "]")?

vector      : "[" (expr ("," expr)*)? "]"

assert      : "->" STRING

TAG         : /@[a-zA-Z]+/

COMMENT     : "--" /[^\n]*/

%import common.ESCAPED_STRING -> STRING
%import common.CNAME
%import common.SIGNED_NUMBER
%import common.WS

%ignore WS
%ignore COMMENT
